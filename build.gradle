buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		mavenCentral()
	}
	dependencies {
		classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.21.0"
		classpath 'org.owasp:dependency-check-gradle:6.5.3'
	}
}

apply plugin: 'org.owasp.dependencycheck'

ext {
	undef = 'undef'
	sharedDir = file('shared')
	coreXsdDir = file('clearth-core/xsd')

	if (!project.hasProperty('cth_version')) {
		cth_version = '1.0.0-SNAPSHOT'
	}

	if (!project.hasProperty('git_commit')) {
		git_commit = ''
	}
	
	if (!project.hasProperty('newProjectDir')) {
		newProjectDir = undef
	}

	oldProjectName = 'newproject'
	if (!project.hasProperty('newProjectName')) {
		newProjectName = undef
	}
	jettyVersion = '9.4.19.v20190610'
	jettyDistName = "jetty-distribution-${jettyVersion}"
}

subprojects {

	repositories {
//		mavenLocal()
		maven {
			name 'MavenLocal' // for local builds only
			url sharedDir
		}
		mavenCentral()
	}
	
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'com.jfrog.artifactory'
	apply plugin: 'maven-publish'
	
	defaultTasks 'build'
	
	compileJava.options.debugOptions.debugLevel = 'source,lines,vars' // Include debug information
	
	ext {
		xsdDir		  = file('xsd')
		genDir		  = file("${buildDir}/generated-src")
		genJavaDir	  = file("${genDir}/java")
		genJaxbDir	  = file("${genDir}/jaxb")
		
		if (!project.hasProperty('versionMark')) {
			versionMark = ''
		}
	}
	
	version	 = cth_version
	group	 = 'com.exactprosystems.clearth'
	
	sourceSets {
		main {
			java {
				srcDir genJavaDir
			}
		}
	}
	
	configurations {
		all {
			transitive = true
			resolutionStrategy {
				// fail eagerly on version conflict (includes transitive dependencies)
				// e.g. multiple different versions of the same dependency (group and name are equal)
				failOnVersionConflict()
				
				// prefer modules that are part of this build (multi-project or composite build) over external modules
//				preferProjectModules()
				
				force   'commons-beanutils:commons-beanutils:1.9.4',
						'commons-io:commons-io:2.7',
						'javax.xml.bind:jaxb-api:2.2',
						'commons-codec:commons-compress:1.21',
						'junit:junit:4.13.1',
						'org.hamcrest:hamcrest-core:1.3',
						'org.slf4j:slf4j-api:1.7.13',
						'org.apache.commons:commons-lang3:3.12.0',
						'org.apache.commons:commons-compress:1.21',
						'commons-logging:commons-logging:1.2',
						'org.apache.logging.log4j:log4j-api:2.17.2',
						'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
				
				
				// don't cache changing modules at all
				cacheChangingModulesFor 0, 'seconds'
			}
		}
		compile.exclude module: 'guava-jdk5'
		compile.exclude group: 'log4j', module: 'log4j'
		compile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
		
		jaxb
	}

	test {
		ignoreFailures = Boolean.getBoolean("test.ignoreFailures")
	}

	task testNg(type: Test) {
		ignoreFailures = Boolean.getBoolean("test.ignoreFailures")
		useTestNG()
	}

	check {
		dependsOn.add("testNg")
	}
	

	dependencies {
		jaxb 'com.sun.xml.bind:jaxb-xjc:2.1.3'
		jaxb 'com.sun.xml.bind:jaxb-impl:2.1.3'
		jaxb 'javax.xml.bind:jaxb-api:2.2'
		jaxb 'javax.xml:jsr173:1.0'

		testCompile 'junit:junit:4.13.1'
		testCompile 'org.mockito:mockito-all:2.0.2-beta'
		testCompile 'org.testng:testng:6.14.3'
		testCompile 'org.xerial:sqlite-jdbc:3.28.0'
		testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
		testCompile 'org.assertj:assertj-core:3.15.0'

		if(JavaVersion.current() > JavaVersion.VERSION_1_8)
			compile 'javax.annotation:javax.annotation-api:1.3.2'

		if (project.getParent().getName() == 'clearth-modules')
			compile project(':clearth-core')
	}

	task createDir {
		doLast {
			genJavaDir.mkdirs()
			genJaxbDir.mkdirs()
		}
	}

	task generateJavaFromXsd(dependsOn: 'createDir', group: 'generation')

	compileJava {
		dependsOn.add('generateJavaFromXsd')
	}

	jar {
		manifest {
			attributes('Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')
			attributes('Specification-Title': '')
			attributes('Specification-Vendor': 'Exactpro Systems LLC')
			attributes('Implementation-Title': "${project.name}")
			attributes('Implementation-Vendor': 'Exactpro Systems LLC')
			attributes('Implementation-Vendor-Id': 'com.exactprosystems')
			attributes('Implementation-Version': "${version}")
			if (versionMark != '')
				attributes('Implementation-Version-Mark': "${versionMark}")
			attributes('Implementation-Version-Date': getDate())
			if (git_commit != '')
				attributes('Git-Commit': "${git_commit}")
		}
	}

	publishing {
		repositories {
			maven {
				name 'sharedDir'
				url sharedDir
			}
		}
	}
}

static def getDate() {
	def date = new Date()
	def formattedDate = date.format('ddMMyyyy-HH:mm:ss.SSS')
	return formattedDate
}

task createCommitMessage {
	doLast {
		def sinceCommit = project.getProperty("since")
		def untilCommit = project.getProperty("until")
		def interval = sinceCommit.toString() + '...' + untilCommit.toString()

		def gitHistory = new ByteArrayOutputStream()
		exec {
			commandLine 'git', 'log', interval, '--grep=^Bug', '--grep=^Feature', '--pretty=format:"%s"'
			standardOutput = gitHistory
		}
		def message = "Applied new Core and modules (" +
				untilCommit.toString() +
				") with the following improvements:\n\n" +
				gitHistory.toString().replace('"', '')

		def commitCommentFile = new File("./core_update")
		try {
			commitCommentFile.write(message.toString())
			println("Core update info has been written to 'core_update' file")
		}
		catch (IOException e) {
			println("Error occured while writing core update info:")
			println(e.toString())
		}
	}
}

task buildCoreArchieves(type: GradleBuild) {
	tasks = ['clearth-core:build', 'clearth-core:uploadArchives',
			 'clearth-modules:clearth-rhdata:build', 'clearth-modules:clearth-rhdata:uploadArchives']
}

project(':clearth-modules') {
	artifactoryPublish.skip = true
}

task assembleAll(group: 'build', description: 'Assembles all ClearTH components, i.e. Core and all of its modules', dependsOn: subprojects*.assemble)
task buildAll(group: 'build', description: 'Builds and tests all ClearTH components, i.e. Core and all of its modules', dependsOn: subprojects*.build)
task publishAll(group: 'publishing', description: 'Publishes all ClearTH components, i.e. Core and all of its modules', dependsOn: subprojects*.publish)

task downloadJetty() {
	def f = new File("${projectDir}/jetty/")
	def jettyDistZip = new File(f, "${jettyDistName}.zip")
	
	outputs.upToDateWhen { jettyDistZip.exists() }
	
	doLast {
		copy {
			def url = new URL("https://repo1.maven.org/maven2/org/eclipse/jetty/jetty-distribution/${jettyVersion}/${jettyDistName}.zip")
			f.mkdirs()
			println 'Downloading Jetty: ' + url
			url.withInputStream { i -> jettyDistZip.withOutputStream { it << i } }
			println 'Unpacking Jetty'
			from zipTree(jettyDistZip)
			into f
			
			def unzipped = new File(f, jettyDistName)
			
			def work = new File(unzipped, 'work')
			println 'Creating directory ' + work
			work.mkdirs()
		}
	}
}

task updateJettyXml(){
	dependsOn 'downloadJetty'
	dependsOn ':clearth-modules:clearth-gui:runJettyXmlUpdater'
 	tasks.findByPath(':updateJettyXml').mustRunAfter(':downloadJetty')
	
	subprojects {
		project ->
			project.afterEvaluate {
				if (project.name == 'clearth-gui') {
					project.setProperty("pathToJettyXml", "${rootDir}/jetty/jetty-distribution-${jettyVersion}/etc/jetty.xml")
					project.setProperty("contextPath", "/clearth/reports")
				}
			}
	}
}

task zipTestReports(type: Zip, group: 'other', description: 'Creates archive with test reports'){
	archiveBaseName = "TestReports"
	archiveAppendix = new Date().format('ddMMyyyy-HH-mm-ss-SSS')
	def reportsDir = "$projectDir/testOutput/SchedulerTestData/automation/reports"
	destinationDirectory = file("$projectDir/testOutput/SchedulerTestData")
	from reportsDir
	
	doLast{
		delete reportsDir
	}
}

task createProject(type: Copy, 
		group: 'ClearTH', 
		description: 'Creates directory with new ClearTH-based project', 
		dependsOn: 'updateJettyXml') {
	
	doFirst {
		if (newProjectDir == undef) {
			throw new GradleException('You must set "newProjectDir" property via "-PnewProjectDir=<path_to_new_project>" key')
		}
		if (newProjectName == undef) {
			throw new GradleException('You must set "newProjectName" property via "-PnewProjectName=<project_name>" key')
		}
		if (!newProjectName.find('^[_*\\w*\\d*]+$')) {
			throw new GradleException("Project name contains illegal characters")
		}
	}
	
	newProjectName = newProjectName.toLowerCase()
	
	outputs.upToDateWhen { false }

	from("${projectDir}/for_new_project/src/main/java/com/exactprosystems/clearth/${oldProjectName}") {
		into "src/main/java/com/exactprosystems/clearth/${newProjectName}"
		filter {
			line -> line.replaceAll(oldProjectName, newProjectName)
		}
	}
	from("${projectDir}/for_new_project/src/main/webapp/") {
		into "src/main/webapp"
		filter {
			line -> line.replaceAll("com.exactprosystems.clearth.${oldProjectName}", "com.exactprosystems.clearth.${newProjectName}")
		}
	}
	from ("${projectDir}/for_new_project") {
		include "build.gradle"
		include "gradle.properties"
		include "jetty94.xml"
		filter( org.apache.tools.ant.filters.ReplaceTokens, tokens: ["JETTY_VERSION": jettyVersion.toString(),
																	 "CORE_SHARED_DIR": sharedDir.toString().replace('\\', '/'),
																	 "CORE_VERSION": cth_version] )
	}
	from (new File("${projectDir}/cfg/")) {
		into "cfg/"
	}
	from (new File("${projectDir}/clearth-modules/clearth-gui/src/main/webapp/")) {
		exclude 'WEB-INF/faces-config.xml'
		into "src/main/webapp/"
	}
	from (new File(projectDir,"jetty/jetty-distribution-${jettyVersion}")) {
		exclude 'demo-base'
		into "jetty"
	}
	from (new File("${projectDir}")) {
		include 'gradle/'
		include 'gradlew.bat'
		include 'gradlew'
	}
	into newProjectDir
}

dependencyCheck {
	skipConfigurations = ['grettyProductRuntime', 'grettyRunnerJetty93', 'grettyRunnerJetty94', 'grettyRunnerJetty8', 'grettyStarter', 
		'grettyRunnerJetty9', 'grettyRunnerTomcat85', 'grettyRunnerTomcat7', 'grettyRunnerTomcat8', 'grettyRunnerJetty7', 'grettyRunnerTomcat9', 
		'grettyNoSpringBoot', 'gretty']
	format = 'ALL'
}

wrapper {
	gradleVersion = '5.5'
}