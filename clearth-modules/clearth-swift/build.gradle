configurations {
	all {
		resolutionStrategy {
			force 'org.apache.commons:commons-lang3:3.12.0'
		}
	}
}
dependencies {
	compile "com.prowidesoftware:pw-swift-core:SRU2021-9.2.8"
}

generateJavaFromXsd {
	inputs.file file("${xsdDir}/swift.xsd")

	outputs.files fileTree("${genJavaDir}/com/exactprosystems/clearth/connectivity/swift").include('*.java')

	doLast {
		ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)

		ant.xjc(schema: "${coreXsdDir}/message.xsd", binding: "${coreXsdDir}/message.xjb",
				destdir: "${genJaxbDir}", package: 'com.exactprosystems.clearth.connectivity.message',
				removeOldOutput: 'yes')
		{
			arg(value: "-episode")
			arg(value: "${genDir}/message.episode")
			produces(dir: "${genJaxbDir}/com/exactprosystems/clearth/connectivity/message", includes: '*')
		}

		ant.xjc(schema: "${xsdDir}/swift.xsd",
				destdir: "${genJavaDir}", package: 'com.exactprosystems.clearth.connectivity.swift',
				removeOldOutput: 'yes', extension: true)
				{
					arg(value: "-catalog")
					arg(value: "${coreXsdDir}/message.cat")
					arg(value: "-b")
					arg(value: "${genDir}/message.episode")
					produces(dir: "${genJavaDir}/com/exactprosystems/clearth/connectivity/swift", includes: '*')
				}
	}
}

publishing {
	publications {
		swift(MavenPublication) {
			groupId = "${group}"
			artifactId = 'clearth-swift'
			version = "${version}"
			
			from components.java
		}
	}
}

artifactoryPublish {
	publications(publishing.publications.swift)
}